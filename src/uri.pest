// SPDX-License-Identifier: GPL-3-0-or-later
// Copyright (c) 2025 Opinsys Oy
// Copyright (c) 2024-2025 Jarkko Sakkinen

uri = { SOI ~ (tpm | file | data | pcr) ~ EOI }

tpm = { "tpm://" ~ "0x" ~ ASCII_HEX_DIGIT+ }

file = { "file://" ~ ANY+ }

data = { "data://" ~ encoding ~ "," ~ ANY* }
encoding = { "utf8" | "hex" | "base64" }

pcr = { "pcr://" ~ selection_body }

selection_body = { bank ~ ("+" ~ bank)* }
bank = { alg ~ colon_and_list }
colon_and_list = { ":" ~ pcr_list }
alg = { "sha1" | "sha256" | "sha384" | "sha512" }
pcr_list = { pcr_index ~ ("," ~ pcr_index)* }
pcr_index = { ("0x" ~ ASCII_HEX_DIGIT+) | ASCII_DIGIT+ }

// Rule for testing to ensure the entire input is consumed.
selection_test = { SOI ~ selection_body ~ EOI }
